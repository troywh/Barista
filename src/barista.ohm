Barista {
  Program   = Statement+
  Statement = Assignment
            | Call
            | stop                                      -- break
            | serve (Exp | strlit)?										  -- return
            | print (Exp | strlit)                      -- print
            | if Exp Block ElIf* (else Block)?          -- ifstmt
            | (true | false) (readonly)?  id					  -- booldec
            | Exp Type(readonly)? id				            -- vardec
            | order id Params FunReturn? Block          -- fundec
            | item id "{" Statement* "}"						    -- classdec
            | blend while Exp Block                     -- while
            | blend Block until Exp                     -- dowhile
            | Assignment
  FunReturn = "->" Type
  ElIf      = else if Exp Block
  Block     = "{" Statement* "}"
  
  Assignment  = Term_id "=" Exp                         -- plain
              | add Exp to Term_id                      -- increment

  Exp         = ("-" | "!") Term                        -- unary
              | OrExp "?" OrExp ":" Exp                 -- ternary
              | OrExp
  OrExp       = OrExp or AndExp                       -- binary
              | AndExp
  AndExp      = AndExp and CmpExp                     -- binary
              | CmpExp
  CmpExp      = AddExp ("less than equal to"
                      | "less than"
                      | "not equal to"
                      | "equal to"
                      | "greater than equal to"
                      | "greater than") AddExp          -- binary
              | AddExp
  AddExp      = AddExp ("+" | "-") MulExp	              -- binary
              | MulExp
  MulExp      = MulExp ("*" | "/" | "%") ExpExp         -- binary
              | ExpExp
  ExpExp      = Term "**" ExpExp                        -- binary
              | Term
  Term        = numlit
              | BoolVal
              | Call
              | ((id | this) ".") id                    -- member
              | id                                      -- id
              | "(" Exp ")"                             -- parens

  Call        = id Args
  Params      = "(" ListOf<Param, ","> ")"
  Param       = Type id
  Args        = "(" ListOf<Arg, ","> ")"
  Arg         = (Exp | strlit)
  BoolVal     = (true | false)
  
  Type	      = id | bool
  
  order       = "order" ~idchar
  while       = "while" ~idchar
  repeat      = "repeat" ~idchar
  until       = "until" ~idchar
  true        = ("true" | "YES" | "Yes" | "yes") ~idchar
  false       = ("false" | "NO" | "No" | "no") ~idchar
  print       = "print" ~idchar
  serve		    = "serve" ~idchar
  if	        = "if" ~idchar
  else        = "else" ~idchar
  blend       = "blend" ~idchar
  stop        = "stop" ~idchar
  add         = "add" ~idchar
  to          = "to" ~idchar
  item        = "item" ~idchar
  this        = "this" ~idchar
  bool        = "with" ~idchar
  and         = "and" ~idchar
  or          = "or" ~idchar
  comment     = "<comment>" | "</comment>"
  readonly    = "*"
  
  keyword   = order | while | true | false | print | serve | if | else | blend | stop | repeat | until | add | to | item | this | comment | bool | and | or
  numlit    = digit+ ("." digit+)?
  strlit		= "\"" (~"\"" (any))* "\""
  id        = ~keyword idchar+
  idchar    = letter | digit | "_"
  space     += "<comment>" (~"</comment>" any)* "</comment>"  -- comment
}